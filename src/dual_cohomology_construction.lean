/-
Copyright (c) 2020 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import abelian
import exact
import pseudoelements

/-!
The construction used by mathlib to define homology is as follows: Find a map
`im_to_ker : image (C.d i) âŸ¶ kernel (C.d (i + 1))` and set `H^i := coker im_to_ker`.

However, we could also use the dual construction: Find a map
`coker_to_coim : cokernel (C.d i) âŸ¶ coimage (C.d i)` and set `H'^i := ker coker_to_coim`.

In this file, we show that these two constructions yield isomorphic cohomology objects. This result
is needed in the construction of the LES in cohomology via the snake lemma.
-/

open category_theory
open category_theory.limits

universes v u

namespace category_theory.abelian

open pseudoelements

section
variables {V : Type u} [ğ’± : category.{v} V] [abelian.{v} V]
include ğ’±

variables {A B C : V} {f : A âŸ¶ B} {g : B âŸ¶ C} (h : f â‰« g = 0)
include h

def im_to_ker : kernel (cokernel.Ï€ f) âŸ¶ kernel g :=
kernel.lift g (kernel.Î¹ (cokernel.Ï€ f)) $
begin
  apply (preadditive.cancel_zero_iff_epi (factor_thru_image f)).1 (by apply_instance),
  rw â†category.assoc,
  rw image.fac,
  exact h,
end

def H := cokernel (im_to_ker h)

def coker_to_coim : cokernel f âŸ¶ cokernel (kernel.Î¹ g) :=
cokernel.desc f (cokernel.Ï€ (kernel.Î¹ g)) $
begin
  apply (preadditive.cancel_zero_iff_mono (factor_thru_coimage g)).1 (by apply_instance),
  rw category.assoc,
  rw coimage.fac,
  exact h,
end

def H' := kernel (coker_to_coim h)

def H_to_coker : (H h) âŸ¶ cokernel f :=
cokernel.desc (im_to_ker h) (kernel.Î¹ g â‰« cokernel.Ï€ f)
begin
  rw â†category.assoc,
  erw limit.lift_Ï€,
  erw kernel.condition,
end

def H_to_H' : H h âŸ¶ H' h :=
kernel.lift (coker_to_coim h) (H_to_coker h)
begin
  apply (preadditive.cancel_zero_iff_epi (cokernel.Ï€ (im_to_ker h))).1 (by apply_instance),
  rw â†category.assoc,
  erw colimit.Î¹_desc,
  dsimp,
  rw category.assoc,
  erw colimit.Î¹_desc,
  erw cokernel.condition,
end

local attribute [instance] object_to_sort
local attribute [instance] hom_to_fun

def ker_to_H' : kernel g âŸ¶ H' h :=
kernel.lift (coker_to_coim h) (kernel.Î¹ g â‰« cokernel.Ï€ f)
begin
  rw category.assoc,
  erw colimit.Î¹_desc,
  erw cokernel.condition,
end

def H_to_H'_other : H h âŸ¶ H' h :=
cokernel.desc (im_to_ker h) (ker_to_H' h)
begin
  apply (preadditive.cancel_zero_iff_mono (kernel.Î¹ (coker_to_coim h))).1 (by apply_instance),
  rw category.assoc,
  erw limit.lift_Ï€,
  dsimp,
  rw â†category.assoc,
  erw limit.lift_Ï€,
  erw kernel.condition,
end

lemma H_to_H'_eq_H_to_H'_other : H_to_H' h = H_to_H'_other h :=
begin
  apply (cancel_mono (kernel.Î¹ (coker_to_coim h))).1,
  apply (cancel_epi (cokernel.Ï€ (im_to_ker h))).1,
  erw limit.lift_Ï€,
  dsimp,
  erw colimit.Î¹_desc,
  dsimp,
  rw â†category.assoc,
  erw colimit.Î¹_desc,
  dsimp,
  erw limit.lift_Ï€,
  refl,
end

instance H_to_H'_epi : epi (H_to_H' h) :=
begin
  rw H_to_H'_eq_H_to_H'_other,
  apply epi_of_pseudo_surjective,
  intro x,
  let y := (kernel.Î¹ (coker_to_coim h) : _ âŸ¶ _) x,
  obtain âŸ¨z, hzâŸ© := pseudo_surjective_of_epi (cokernel.Ï€ f) y,
  have : cokernel.Ï€ f â‰« coker_to_coim h = cokernel.Ï€ (kernel.Î¹ g),
  { erw colimit.Î¹_desc, refl, },
  have : (cokernel.Ï€ (kernel.Î¹ g) : _ âŸ¶ _) z = 0,
  { erw [â†this, comp_apply, hz, â†comp_apply, kernel.condition, zero_apply], },
  obtain âŸ¨a, haâŸ© := (pseudo_exact_of_exact (cokernel_exact _)).2 _ this,
  use (cokernel.Ï€ (im_to_ker h) : _ âŸ¶ _) a,
  erw [â†comp_apply, colimit.Î¹_desc],
  dsimp,
  apply pseudo_injective_of_mono (kernel.Î¹ (coker_to_coim h)),
  erw [â†comp_apply, limit.lift_Ï€],
  dsimp,
  rw [comp_apply, ha, hz],
end

instance H_to_H'_mono : mono (H_to_H' h) :=
begin
  apply mono_of_zero_of_map_zero,
  intros a ha,
  obtain âŸ¨b, hbâŸ© := pseudo_surjective_of_epi (cokernel.Ï€ (im_to_ker h)) a,
  let c := (kernel.Î¹ g : _ âŸ¶ _) b,
  suffices : (cokernel.Ï€ f : _ âŸ¶ _) c = 0,
  { obtain âŸ¨d, hdâŸ© := (pseudo_exact_of_exact (kernel_exact _)).2 _ this,
    have hc : c = (kernel.Î¹ g : _ âŸ¶ _) b := rfl,
    rw â†hb,
    have : (im_to_ker h) d = b,
    { apply pseudo_injective_of_mono (kernel.Î¹ g) _,
      rw â†comp_apply,
      erw limit.lift_Ï€,
      dsimp,
      rw [hd, hc], },
    rw [â†this, â†comp_apply, cokernel.condition, zero_apply] },
  have : (kernel.Î¹ (coker_to_coim h) : _ âŸ¶ _) ((H_to_H' h) a) = 0,
  { erw [ha, apply_zero], },
  conv_lhs at this { erw [â†comp_apply, limit.lift_Ï€], dsimp,
    erw [â†hb, â†comp_apply, colimit.Î¹_desc], dsimp,
    rw comp_apply },
  exact this,
end

instance H_to_H'_iso : is_iso (H_to_H' h) :=
mono_epi_iso _

def H_iso_H' : H h â‰… H' h :=
as_iso $ H_to_H' h

end

end category_theory.abelian
